//file:noinspection all

plugins {
  id 'fabric-loom' version '0.10-SNAPSHOT'
  id 'maven-publish'
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17
archivesBaseName = "${project.archives_base_name}-${project.minecraft_version}"
group = project.maven_group

version = System.env.GITHUB_ACTIONS == "true"
        ? "${project.mod_version}+build.${System.env.GITHUB_RUN_NUMBER}"
        : project.mod_version

repositories {
  maven {
    name = "Quilt Maven"
    url  = "https://maven.quiltmc.org/repository/release/"
  }

  maven {
    name = "Modrinth"
    url  = "https://api.modrinth.com/maven/"
  }

  maven {
    name = "Terraformers Maven"
    url  = "https://maven.terraformersmc.com/releases/"
  }

  maven {
    name = "Terraformers Backup Maven"
    url  = "https://maven.terraformersmc.com/releases/"
  }

  maven {
    name = "Shadew's Maven"
    url  = "https://maven.shadew.net/"
  }

  maven {
    name = "Modding Playground's Maven"
    url = "https://raw.githubusercontent.com/moddingplayground/maven/main/"
  }

  maven {
    name = "Gegy's Maven"
    url  = "https://maven.gegy.dev/"
  }

  maven {
    name = "Ladysnake Mods"
    url  = "https://ladysnake.jfrog.io/artifactory/mods/"
  }

  maven {
    name = "Shedaniel"
    url  = "https://maven.shedaniel.me/"
  }

  maven {
    name = "Artifactory"
    url  = "https://server.bbkr.space/artifactory/libs-release/"
  }
}

dependencies {
  minecraft "com.mojang:minecraft:${project.minecraft_version}"
  mappings "net.fabricmc:yarn:${project.minecraft_version}+build.${project.yarn_build}:v2"
  modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

  modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

  String            cardinalComponents       = "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-%s:${project.cardinal_version}"
  String            cardinalComponentsBase   = cardinalComponents.formatted("base")
  modImplementation cardinalComponentsBase ; include cardinalComponentsBase
  String            cardinalComponentsEntity = cardinalComponents.formatted("entity")
  modImplementation cardinalComponentsEntity ; include cardinalComponentsEntity

  String  clothConfig = "me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}"
  modApi  clothConfig ; include clothConfig

  String frame = "net.moddingplayground.frame:frame-fabric:${project.frame_version}"
  modImplementation frame ; include frame

  String toymaker = "net.moddingplayground.toymaker:toymaker-fabric:${project.toymaker_version}"
  modImplementation toymaker ; include toymaker

  modImplementation "com.terraformersmc:modmenu:${project.mod_menu_version}"

  modImplementation "maven.modrinth:sodium:${project.sodium_version}"
  implementation    "org.joml:joml:${project.joml_version}"
}

loom {
  accessWidenerPath = file("src/main/resources/${project.mod_id}.accesswidener")

  runs {
    data {
      server()

      vmArgs(
              "-Dtoymaker.datagen=true",
              "-Dtoymaker.datagen.modid=${project.mod_id}",
              "-Dtoymaker.datagen.path=\"${project.file("src/generated/resources")};${project.file("$buildDir/resources/main")}\""
      )
    }
  }
}

processResources {
  inputs.property "version", project.version
  filesMatching("fabric.mod.json") { expand "version": project.version }
}

sourceSets.main.resources { srcDirs += "$projectDir/src/generated/resources" }
tasks.withType(JavaCompile).configureEach { it.options.release = 17 }
java { withSourcesJar() }

jar {
  from("LICENSE") { rename { "${it}_${project.mod_id}" } }
  from("LICENSE_ASSETS") { rename { "${it}_${project.mod_id}" } }
}

publishing { publications { mavenJava(MavenPublication) { from components.java } } }
